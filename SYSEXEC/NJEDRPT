/* rexx */
/*********************************************************************/
/* used by the nj command to chop up the jclcheck reports.           */
/*********************************************************************/
Address ISREDIT
"MACRO"
"(LAST) = LINENUM .ZLAST"
If LAST = 0 Then Do
  Say 'No report generated from JCLCHECK. Aborting.'
  Return
End

"FIND ' REPORT 1 ' FIRST"
"(LINE) = LINENUM .ZCSR"
Do LINE - 1
  "DELETE 1"
End

"FIND 'SYMBOLICS DEFINED ' 2 FIRST"
If RC = 0 Then Do
  "(LINE1) = LINENUM .ZCSR"
  If LINE1 > 0 Then Do
    "FIND '******************' 2 NEXT"
    "(LINE2) = LINENUM .ZCSR"
    Do LINE2 - LINE1
      "DELETE" LINE1
    End
  End
End

/* exclude all the jclcheck headings */
"X '           PAGE    ' 2 ALL"
"X 'CA-JCLCHECK' 2 ALL"
"X '-------------------' 2 ALL"
"X '....+....1' 12 ALL"
"X '  STMT  ' 2 ALL"
"X '  NUM   ' 2 ALL"
"X ' DSET ' 54 ALL"
"X 'DSNAME         ' 2 ALL"
"X 'PROGRAM   LIBRARY' 2 ALL"
"X 'NAME      VOLUME ' 2 ALL"
"X 'PROCEDURE   LIBRARY' 2 ALL"
"X 'NAME        VOLUME ' 2 ALL"
"X 'CAY6000 ' 2 ALL"
"X '#####################' 2 ALL"

/* exclude all the blank lines */
"X '"Copies(' ',133)"' ALL"

/* exclude the comments from report 1 since they can change order */
"X ' //*' 11 ALL"
"X ' //*' 37 ALL"

/* delete them */
"DELETE ALL X"

/* leave the first heading for each report */
"CURSOR = 1 1"
"X 'COMPUTER ASSOCIATES' 2 ALL"
"F 'COMPUTER ASSOCIATES' 2 FIRST"
Do Forever
  "F '*********************' 2 NEXT"
  If RC > 0 Then Leave
  "F 'COMPUTER ASSOCIATES' 2 NEXT"
End
"X '*********************' 2 ALL"

/* delete them */
"DELETE ALL X"

/* make the report breaks a little easier to read */
"C 'COMPUTER ASSOCIATES' '*******************' ALL 2"

"SAVE"
"END"

Return
