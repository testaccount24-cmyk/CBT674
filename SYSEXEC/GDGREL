/* rexx */
Arg GdgDsn .

Signal On Failure
Signal On Syntax
Signal On Novalue

Return Convert_Gdg()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit '';
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit '';
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit '';
Cleanup:
Say 'Unable to convert relative GDG' GdgDsn 'to a real name.'
Return
/*-------------------------------------------------------------------*/
/*- The main exec routine.                                          -*/
/*-------------------------------------------------------------------*/
Convert_Gdg:

GdgDsn = PrefDsn(GdgDsn)

Parse Var GdgDsn Name '(' Gen ')'
If Datatype(Gen) <> 'NUM' Then Signal Failure
If Gen > 0 Then Do
  Say 'Relative generation' Gen 'changed to generation 0.'
  Gen = 0
End

/* list all gdgs */
Address TSO
X=OUTTRAP("L.")
"LISTCAT ENTRY('"Name"')"
X=OUTTRAP("OFF")

/* make sure we get a good return from the listcat */
If RC <> 0 Then Do
  Say "LISTCAT return code for" GdgDsn "=" RC
  Signal Failure
End

/* make sure we get some output from the listcat */
If L.0 < 1 Then Do
  Say "LISTCAT returned no entries for" GdgDsn
  Signal Failure
End

/* make the first line starts with GDG BASE */
/* GDG BASE ------ MA133.TEST.GDG */
If Substr(L.1,1,8) <> "GDG BASE" Then Do
  Say GdgDsn "is not a gdg base."
  Signal Failure
End

/* look for any nonvsam files returned from the list command */
/* NONVSAM ------- MA133.TEST.GDG.G0007V00 */
iGen = 0
Do iL = 1 to L.0
  If Word(L.iL,1) <> 'NONVSAM' Then Iterate
  /* pull the dataset name from the list command */
  iGen = iGen + 1
  Gen.iGen = Word(L.iL,(Words(L.iL)))
End
Gen.0 = iGen

If Gen.0 = 0 Then Do
  Say 'No generations exist for' GdgDsn
  Signal Failure
End

WantGen = Gen.0 + Gen

If WantGen < 1 Then Do
  Say 'Generation' Gen 'does not exist for' GdgDsn
  Say 'Oldest generation is -'Gen.0
  Signal Failure
End

Return Gen.WantGen
