/* rexx */
/*-------------------------------------------------------------------*/
/*- list the concatenations for a passed dd name.                   -*/
/*-------------------------------------------------------------------*/
Arg DDName .

x = Outtrap('Dsn.')
"LISTA STATUS"
x = Outtrap('OFF')

OldDD = ''
Do iDsn = 1 To Dsn.0
  Dsn = Dsn.iDsn
  If Left(Dsn,1) = '-' Then Iterate
  If Left(Dsn,1) = ' ' Then Iterate
  If Pos('(',Dsn) > 0 Then Iterate /* for pds(mem) allocations */
  Dsn = Strip(Dsn)

  /* we know we'er working with a dataset now. */
  /* look ahead to see if there's a dd.        */
  iDD = iDsn + 1
  DD = Dsn.iDD
  Parse Var DD DD 'KEEP'
  DD = Strip(DD)
  If Words(DD) > 1 Then Iterate

  If DD = DDName Then OldDD = DD        /* we're interested */
  Else If DD /= '' Then OldDD = ''        /* not interested */

  /* OldDD is something, so add it to our list */
  If OldDD /= '' Then Say Dsn
End

Return
/* sample output from the lista status command */
--DDNAME---DISP--
MA133.TSO.PROD.SYSEXEC
  SYSEXEC  KEEP
SYS2.UTIL.REXX
           KEEP
SYS9.CYFUSION.EXECS
           KEEP
SYS9.SOFTPUB.EXEC
           KEEP
SYS9.XMITIP.NEW.EXEC
           KEEP
SYS1.SBPXEXEC
           KEEP
SYS9.CAI.OPSMVS.REXX
           KEEP
FFST.V120ESA.SEPWSRC1
           KEEP
