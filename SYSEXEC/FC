/* rexx */
Arg Job Func

Signal On Failure
Signal On Syntax
Signal On Novalue

Call Main_Routine

Return
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Return
/*-------------------------------------------------------------------*/
/*- This exec will look for job documentation from the word pointed -*/
/*- to by the cursor, or by the passed doc member.                  -*/
/*-------------------------------------------------------------------*/
Main_Routine:

Parmlib = "MA133.TSO.PARMLIB(FC)"

If Length(Job) = 1 Then Do                  /* if job is one long    */
  Func = Job                                /* assume it's the func  */
  Job = ''
End

Address ISPEXEC
If Job = '' Then Do                         /* user passed a job?    */
  GETLINE = ''                              /* get cursor line.      */
  "VPUT GETLINE SHARED"
  "SELECT CMD(%GETLINE)"
  "VGET (CSR CSRP LINE SCRW PANEL) SHARED"

  Upper LINE
  CmdPos = Pos('COMMAND',LINE)              /* cursor on cmd line?   */
  If CmdPos = 0 | CmdPos > 3 Then Do
    If Pos('//',LINE) > 0 & Pos(' JOB ',LINE) > 0 Then Do
      Parse Var LINE '//' Job .             /* find the job          */
      Job = Strip(Job)
    End
    Else Do
      Do While CSRP > 1                     /* get the word from csr */
        If Substr(Line,CSRP-1,1) = ' ' Then Leave
        CSRP = CSRP - 1
      End
      Job = Word(Substr(Line,CSRP),1)
      Job = Strip(Job,'L','/')              /* remove any slashes    */
    End
  End
End
Address TSO

If Job = '' Then Do                         /* still can't find job? */
  Say 'Enter the doc name you want to find...'
  Pull Job
End
If Job = '' Then Do                         /* give up.              */
  Say 'Doc name is missing.'
  Return
End

"ALLOC F(IN) DA('"Parmlib"') SHR REUSE"
"EXECIO * DISKR IN (STEM Dsn. FINIS"
"FREE F(IN)"

Sysname = Mvsvar('SYSNAME')
Edit.0 = 0                                  /* add doc libs to search*/
Do iDsn = 1 To Dsn.0
  Dsn.iDsn = Strip(Dsn.iDsn)
  Upper Dsn.iDsn
  If Left(Dsn.iDsn,1) = '*' Then Iterate
  Parse Var Dsn.iDsn Sys Dsn .
  If Sys <> Sysname & Sys /= 'ALL' Then Iterate
  Dsn = Strip(Dsn,"B","'")
  Call CheckDsn Dsn
End

If Edit.0 = 0 Then Do
  Say 'Doc' Job 'not found.'
  Return
End

If Edit.0 = 1 Then Do
  Dsn = Edit.1 || "(" || Job || ")"
  Call FDSN "'"Dsn"'" Func
  Return
End

Say Job 'was found in the following doc libraries...'
Do iEdit = 1 To Edit.0
  Say iEdit Edit.iEdit
End
Say 'Reply with a valid library number, or anything else to quit.'
Pull Reply
Reply = Strip(Reply)
If Datatype(Reply,'N') = 1 & Reply > 0 & Reply <= Edit.0 Then Do
  Dsn = Edit.Reply || "(" || Job || ")"
  Call FDSN "'"Dsn"'" Func
End

Return
/*-------------------------------------------------------------------*/
/*- check to see if the dsn exists and edit it                      -*/
/*-------------------------------------------------------------------*/
CheckDsn:

Arg Dsn
CheckDsn = "'" || Dsn || "(" || Job || ")'"
If SYSDSN(CheckDsn) <> 'OK' Then Return
iEdit = Edit.0
iEdit = iEdit + 1
Edit.iEdit = Dsn
Edit.0 = iEdit

Return
