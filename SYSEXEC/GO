/* rexx */
/*********************************************************************/
/* this allows you to run an exec while editing it. if there are     */
/* outstanding changes to the exec, it will save them first.         */
/*********************************************************************/
Address ISREDIT
"MACRO (PARMS)"
"(DSN) = DATASET"
"(MEM) = MEMBER"

/* save any outstanding changes before continuing */
"(CHG) = DATA_CHANGED"
If CHG = 'YES' Then Do
  "SAVE"
  If RC > 4 Then Do
    If MEM <> '' Then Do
      Say "SAVE failed, ya want I should compress this? (Y or N)"
      Pull Answer
      If Substr(Answer,1,1) = 'Y' Then Do
        "COMPRESS"
        "SAVE"
        If RC > 4 Then Do
          Say "SAVE still fails, aborting GO command..."
          Exit 8
        End
      End
    End
    Else Do
      Say "SAVE failed, aborting GO command..."
      Exit 8
    End
  End
End

Address ISPEXEC "CONTROL ERRORS RETURN"
Command = DSN
If MEM <> '' Then Command = Command || '(' || MEM || ')'
Address TSO "EX '"Command"' '"PARMS"'"
If RC > 0 Then Say "Command ended with RC="RC

Return
