/* rexx */
Arg Dsn Func .

Signal On Failure
Signal On Syntax
Signal On Novalue

Call Main_Routine

Return
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Return
/*-------------------------------------------------------------------*/
/*- This will find a dataset name from an edit session or any ispf  -*/
/*- screen and perform the specified action on it.                  -*/
/*-------------------------------------------------------------------*/
Main_Routine:

Address ISPEXEC

Edit = 'Y'
"ISREDIT MACRO (DSN,FUNC)"
If RC <> 0 Then Edit = 'N'

If Length(Dsn) = 1 Then Do
  Func = Dsn
  Dsn = ''
End
Dsn = Prefdsn(Dsn)

If Dsn = '' & Edit = 'Y' Then Do
  "ISREDIT ZOOM" Func
  Exit RC
End

If Dsn = '' Then Do          /* was the dsn passed? */
  GETLINE = ''
  "VPUT GETLINE SHARED"
  "SELECT CMD(%GETLINE)"
  "VGET (CSR CSRP LINE SCRW PANEL) SHARED"
  Upper LINE

  CmdPos = Pos('COMMAND',LINE)
  If CmdPos = 0 | CmdPos > 3 Then Do
    If Pos('DSNAME=',LINE) > 0 Then CSRP = Pos('DSNAME=',LINE) + 7
    Else If Pos('DSN=',LINE) > 0 Then CSRP = Pos('DSN=',LINE) + 4

    Valid = "ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$0123456789.-+()"
    Len = Length(LINE)
    Beg = Verify(Reverse(LINE),Valid,"N",(Len - CSRP + 1))
    If Beg = 0 Then Beg = 1
    Else Beg = Len - Beg + 2
    End = Verify(LINE,Valid,"N",CSRP)
    If End = 0 Then End = Len + 1
    Dsn = Substr(LINE,Beg,Abs(End-Beg))

    If Right(Dsn,2) = '))' Then Dsn = Left(Dsn,Length(Dsn)-1)
    If Left(Dsn,1) = '(' Then Dsn = Substr(Dsn,2)
    If Right(Dsn,1) = ')' & Pos('(',Dsn) = 0 Then
      Dsn = Left(Dsn,Length(Dsn)-1)
  End
End

If Dsn = '' Then Do
  Say 'Enter the dataset name you want to use...'
  Pull Dsn
  Dsn = Prefdsn(Dsn)
End
If Dsn = '' Then Do
  Say 'Dataset name is missing.'
  Return
End

If Left(Dsn,1) <> "'" Then Dsn = "'"Dsn"'"

Call FDSN Dsn Func

Return
