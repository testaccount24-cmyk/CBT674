/* rexx */
/*********************************************************************/
/* used in the srchfor dialogs (usually opt 3.14). set your search   */
/* 'output mode' (on lower right corner of the srchfor main panel)   */
/* to 'view'. when viewing the search results, set a pfkey to        */
/* 'srchgo view' or 'srchgo edit' and place your cursor on one of the*/
/* found lines and hit the pfkey.                                    */
/*********************************************************************/
Address ISREDIT
"MACRO (EDITVIEW)"
"(LINE) = LINE 2"
Parse Var LINE ' SRCH DSN: ' Dsn .
Dsn = Strip(Dsn)
If Dsn = '' Then Do
  Say 'Unable to determine SRCHFOR DSN.'
  Return
End

"(CSRNUM) = LINENUM .ZCSR"
Member = ''
Do iLine = CSRNUM To 1 By -1
  "(LINE) = LINE" iLine
  If Pos('STRING(S) FOUND',LINE) > 0 Then Do
    Member = Word(LINE,1)
    Leave
  End
End
If Member /= '' Then Dsn = Dsn"("Member")"

"(LINE) = LINE .ZCSR"
Parse Var LINE 2 Pos 9
Pos = Strip(Pos)
If Pos = '' Then Pos = 1
If Datatype(Pos,'N') = 0 Then Pos = 1

If EditView = '' Then EditView = 'V'
EditView = Left(EditView,1)
FDSNMAC = 'SRCHEDIT'
Address ISPEXEC "VPUT (POS FDSNMAC) SHARED"
Call FDSN "'"Dsn"'" EditView

Return
