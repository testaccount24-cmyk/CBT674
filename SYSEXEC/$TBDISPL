/*-------------------------------------------------------------------*/
/* Use this routine as a model to display any ispf table.            */
/* Place your own primary command and line commands in the areas     */
/* provided. This model allows multiple line command selections.     */
/* When all processing is complete, it will redisplay the table at   */
/* the same position as the last display. One primary command is     */
/* provided here, 'SORTCOMP' which will perform a complex sort on    */
/* your table, promting the user to specify ascending and decending  */
/* attributes for each column. See the 'SORTTB' exec for more.       */
/* Change TabName to the table name you want to display.             */
/*-------------------------------------------------------------------*/

TabName = My_Table_Name

Display_Table:

Address ISPEXEC
CURSOR = 'LC'; CSRROW = 0; ZTDTOP = 0;
Do Forever
  "TBTOP" TabName
  "TBSKIP" TabName "NUMBER("ZTDTOP") NOREAD"
  "TBDISPL" TabName "PANEL("TabName") POSITION(CRP) ROWID(ROW)" ,
    "AUTOSEL(NO) CSRROW("CSRROW") CURSOR("CURSOR")"
  If RC > 4 Then Leave
  CURSOR = 'ZCMD'

  If ZCMD /= '' Then Do        /* primary commands go here */
    Upper ZCMD
    Select
      When ZCMD = 'SORTCOMP' Then Call Sorttb TabName /* allows sort*/
      Otherwise Nop
    End
  End

  Do While ZTDSELS > 0       /* line commands go here */
    Upper LC                 /* this loop allows multiple selecitons */
    Select
      When LC = 'S' Then Call Select
      Otherwise Nop
    End
    If ZTDSELS > 1 Then "TBDISPL" TabName "POSITION(CRP) ROWID(ROW)"
    Else ZTDSELS = 0
    CURSOR = 'LC'
  End
  LC = ' '
  "TBGET" DumpTab "POSITION(CSRROW)"       /* place cursor properly */
End

Return
