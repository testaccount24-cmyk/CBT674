/* rexx */
Arg Dsn Disp Echo .

Signal On Failure
Signal On Syntax
Signal On Novalue

Call Main_Routine

Return
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Say '*** Library compress failed!'
If Echo = '' & Symbol('Sysprint') = 'VAR' Then Do
  Say 'Compress information written to' Sysprint
  If Sysvar('SYSISPF') = 'ACTIVE' Then Call FDSN "'"Sysprint"' B"
End
Return
/*-------------------------------------------------------------------*/
/*- This will compress a library. can be run as an edit macro or as -*/
/*- a tso exec. disp is the disposition of the compressed lib (old  -*/
/*- or shr). echo will write out the iebcopy messages to your screen-*/
/*- if it is non-blank.                                             -*/
/*-------------------------------------------------------------------*/
Main_Routine:

/* see if we're running as an edit macro */
If Sysvar('SYSISPF') = 'ACTIVE' Then Do
  Address ISREDIT
  "MACRO (MACDISP MACECHO)"
  If RC = 0 Then Do
    /* yep, load the vars */
    Disp = MACDISP
    Echo = MACECHO
    "(DSN) = DATASET"
    Dsn = "'"Dsn"'"
  End
End
Address TSO

/* make sure our disp is ok */
If Disp = '' Then Disp = 'SHR'
If Disp /= 'OLD' & Disp /= 'SHR' Then Do
  Say 'Invalid DISP passed:' Disp'. Must be OLD, SHR, or null.'
  Signal Failure
End

/* if we're running as a batch job, just echo commands to systsprt */
If Echo = '' & Sysvar('SYSENV') = 'BACK' Then Echo = 'Y'

/* make sure our dsn is ok */
Dsn = PrefDsn(Dsn)
Call Check_Lib

/* ready to rock */
"ALLOC F(PDS)   DA('"Dsn"')" Disp "REU"
"ALLOC F(SYSIN) DA(ISPST2) NEW DELETE REU" ,
  "UNIT(TEST)   SPACE(1)   TRACKS" ,
  "LRECL(80)    DSORG(PS)  RECFM(F B)"

/* write results to a file if echo is set to no */
If Echo = '' Then Do
  Sysprint = PrefDsn('SYSPRINT.COMPRESS')
  If Sysdsn("'"Sysprint"'") /= 'OK' Then
    "ALLOC F(SYSPRINT) DA('"Sysprint"') RECFM(F B A)" ,
      "LRECL(121) SPACE(1 1) TRACK REUSE"
  Else "ALLOC F(SYSPRINT) DA('"Sysprint"') SHR REUSE"
End
Else "ALLOC F(SYSPRINT) DA(*) REU"

/* do the compress */
Queue '  COPY  I=((PDS,R)),O=PDS'
"EXECIO 1 DISKW SYSIN (FINIS"
"TSOEXEC CALL *(IEBCOPY)"
SaveRC = RC
"FREE F(PDS SYSIN SYSPRINT)"
If SaveRC > 4 Then Signal Failure

Say "Compress of '"Dsn"' successful."

If Echo = '' Then Do
  x = Msg('OFF')
  "DELETE '"Sysprint"'"
  x = Msg(x)
End

Return
/*-------------------------------------------------------------------*/
/*- Double check that we're dealing with a valid lib                -*/
/*-------------------------------------------------------------------*/
Check_Lib:

CC = Listdsi("'"Dsn"'")
If CC > 0 Then Do
  Say "ListDsi failed for '"Dsn"', CC="CC", SYSREASON="SYSREASON
  Signal Failure
End
If SYSDSORG /= 'PO' Then Do
  Say "DSORG value of" SYSDSORG "must be PO for dataset '"Dsn"'"
  Signal Failure
End

Return
