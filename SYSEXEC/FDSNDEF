/* rexx */
/*********************************************************************/
/* works with the FDSN exec to establish a default action. this is   */
/* so you won't have to specify the same action command every time   */
/* you run FDSN. optionally specify a one-time edit macro to run     */
/* when asking for edit/view. this is intended more for programmatic */
/* calls to run a macro for each member of a pds. the macro is not   */
/* saved permanently and only runs once so that you won't run it by  */
/* accident and screw up an important dataset.                       */
/*********************************************************************/
Arg FDSNDEF FDSNMAC .

If Sysvar('SYSISPF') <> 'ACTIVE' Then Do
  Say 'ISPF is not active for FDSN'
  Return
End

Address ISPEXEC
"VGET ZAPPLID"                      /*   Get current application ID */
If ZAPPLID \= 'ISR' Then Do         /*   If ZAPPLID\=ISR, re-invoke */
  "SELECT CMD(%FDSNDEF" FDSNDEF FDSNMAC") NEWAPPL(ISR)"
  Exit RC                           /*   with NEWAPPL(ISR) so that  */
End                                 /*   pfkeys will be predictable */

Address ISPEXEC
If FDSNDEF = '' Then "VERASE FDSNDEF PROFILE"
Else "VPUT FDSNDEF PROFILE"

If FDSNMAC = '' Then "VERASE FDSNMAC SHARED"
Else "VPUT FDSNMAC SHARED"

Return
