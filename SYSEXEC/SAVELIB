/* rexx */
Arg InLib Echo .

Signal On Failure
Signal On Syntax
Signal On Novalue

Call Main_Routine

Return
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Say '*** Library copy failed!'
Return
/*-------------------------------------------------------------------*/
/*- This will save a library to libname.savelib. it's a quick and   -*/
/*- dirty copy of a lib.                                            -*/
/*-------------------------------------------------------------------*/
Main_Routine:

InLib = PrefDsn(InLib)
OutLib = InLib".SAVED"
If Length(OutLib) > 44 Then Do
  Say '*** Length of output library > 44 chars. Do it manually.'
  Signal Failure
End
Call Check_Lib

If Sysdsn("'"OutLib"'") <> 'DATASET NOT FOUND' Then Do
  "DELETE '"OutLib"'"
  If RC <> 0 Then Signal Failure
End

"ALLOC F(SYSUT1) DA('"InLib"') SHR REU"
"ALLOC F(SYSUT2) DA('"OutLib"') NEW CATALOG LIKE('"InLib"')"
"ALLOC F(SYSIN) DA(ISPST2) NEW DELETE REU" ,
  "UNIT(TEST)   SPACE(1)   TRACKS" ,
  "LRECL(80)    DSORG(PS)  RECFM(F B)"
If Echo = '' Then "ALLOC F(SYSPRINT) DUMMY REU"
Else "ALLOC F(SYSPRINT) DA(*) REU"

Queue '  COPY  I=((SYSUT1,R)),O=SYSUT2'
"EXECIO 1 DISKW SYSIN (FINIS"
"TSOEXEC CALL *(IEBCOPY)"
SaveRC = RC
"FREE F(SYSUT1 SYSUT2 SYSIN SYSPRINT)"
If SaveRC > 4 Then Signal Failure

Return
/*-------------------------------------------------------------------*/
/*- Double check that we're dealing with a valid lib                -*/
/*-------------------------------------------------------------------*/
Check_Lib:

Address TSO
CC = Listdsi("'"InLib"'")
If CC > 0 Then Do
  Say "ListDsi failed for '"InLib"', CC="CC", SYSREASON="SYSREASON
  Signal Failure
End
If SYSDSORG <> 'PO' Then Do
  Say 'DSORG value of' SYSDSORG 'must be PO'
  Signal Failure
End

Return
