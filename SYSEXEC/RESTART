/* rexx */
/******************************************************************/
/* this exec is to be used on the iof job summary display for     */
/* scanning the messages dataset to find out if there are any     */
/* datasets that need to be uncataloged for a restart of the job. */
/* by default, it scans for all datasets, but you can pass the    */
/* dataset prefixes you want scanned for (ie '%restart shr xfer'  */
/* to check for shr and xfer datasets. passing 'rename' as one    */
/* of the args will set up rename commands instead of delete.     */
/******************************************************************/
Arg ArgPrefs
Action = 'D'
RenPos = Pos('RENAME',ArgPrefs)
If RenPos > 0 Then Do
  Action = 'R'
  Parse Var ArgPrefs ArgPrefs1 'RENAME' ArgPrefs2
  ArgPrefs = Strip(ArgPrefs1) Strip(ArgPrefs2)
End
ArgPrefs = Strip(ArgPrefs)

/******************************************************************/
/* break out the prefixes to the ones we want to check.           */
/******************************************************************/
Prefs = ArgPrefs
iPref = 0
Do While Words(Prefs) > 0
  Parse Var Prefs Pref Prefs
  iPref = iPref + 1
  Pref.iPref = Strip(Pref,'T','*')
End
Pref.0 = iPref

/******************************************************************/
/* check to make sure user is on correct panel.                   */
/******************************************************************/
Address IOF
"TSICOPY NAME(HELP) TO(REXX) SECTION(PANEL)"
If HELP <> 'JOBSUM' Then Do
  Say "You must be on the 'IOF Job Summary' panel to run RESTART."
  Return
End

/******************************************************************/
/* get the jobname for the comment section of the job.            */
/******************************************************************/
"1"
"TSICOPY NAME(JOBNAME JOBID) TO(REXX) SECTION(PANEL)"
"END"
JOBID = 'J'Right(JOBID,5)                    /* shorten the jobid */

/******************************************************************/
/* go to the job message dataset and read it in.                  */
/******************************************************************/
"3 ALLOC DDNAME(MESSAGES)"
Address TSO
"EXECIO * DISKR MESSAGES (STEM Msg. FINIS"
"FREE F(MESSAGES)"

/******************************************************************/
/* scan thru and check for cataloged messaqes, save the dsns.     */
/******************************************************************/
iDsn = 0
iStep = 0
Do iMsg = 1 To Msg.0
  Parse Var Msg.iMsg Msgid .
  Select
    When Msgid = 'IEF236I' Then Do
      Dsn.iStep.0 = iDsn
      iDsn = 0
      Parse Var Msg.iMsg . . . . PStep JStep
      If JStep <> '' Then ,
        Step = ' JOB STEP:' JStep ' PROC STEP:' PStep
      Else Step = ' JOB STEP:' PStep
      iStep = iStep + 1
      Step.iStep = Step
      Iterate
    End
    When Msgid = 'DSN' Then Do
      Dsn = Word(Msg.iMsg,2)
      If Left(Dsn,1) <> '(' Then Iterate
      Parse Var Dsn '(' Dsn ')'
      Dsn = Strip(Dsn)
    End
    When Msgid = 'IGD108I' Then Do
      Parse Var Msg.iMsg . Dsn Disp ','
      If Disp <> 'CATALOGED' Then Iterate
    End
    When Msgid = 'IEF285I' Then Do
      If Word(Msg.iMsg,3) <> 'CATALOGED' Then Iterate
      Dsn = Word(Msg.iMsg,2)
    End
    Otherwise Iterate
  End
  /****************************************************************/
  /* check to ensure this isn't a work dsn.                       */
  /****************************************************************/
  If Left(Dsn,3) = 'SYS' Then Do
    Parse Var Dsn 'SYS' Date '.T' Time '.'
    If Length(Date) = 5 & Length(Time) = 6 & ,
      Datatype(Date) = 'NUM' & Datatype(Time) = 'NUM' Then Iterate
  End
  /****************************************************************/
  /* see if the dsn extracted matches one of the prefixes.        */
  /****************************************************************/
  If Pref.0 > 0 Then Do iPref = 1 To Pref.0
    If Pos(Pref.iPref,Dsn) = 1 Then Do
      iDsn = iDsn + 1
      Dsn.iStep.iDsn = Strip(Dsn)
      Leave
    End
  End
  Else Do
    iDsn = iDsn + 1
    Dsn.iStep.iDsn = Strip(Dsn)
  End
End
Step.0 = iStep
Dsn.iStep.0 = iDsn

/******************************************************************/
/* if there were no dsns found, there is nothing further to do.   */
/******************************************************************/
Total = 0
Do iStep = 1 To Step.0
  Total = Total + Dsn.iStep.0
End

If Total = 0 Then Do
  If ArgPrefs = '' Then ,
    Say 'There were no datasets found.'
  Else Say 'There were no datasets that began with' ArgPrefs'.'
  Return
End

/******************************************************************/
/* build an idcams job to delete the found datasets. if the user  */
/* has a valid job card in the list/log panel, use it, otherwise  */
/* build a default job card.                                      */
/******************************************************************/
Address IOF
Out.0 = 0
Do i = 1 To 4
  "TSICOPY NAME(ZLLGJOB"i") FROM(DIALOG) TO(REXX)"
  Jc = Value("ZLLGJOB"i)
  If RC > 0 | Jc = '' Then Leave
  If Left(Jc,3) = '//*' Then Iterate
  If Pos(" JOB ",Jc) > 0 Then Do
    Parse Var Jc ' JOB ' '(' Account ')'
    If Account = "" Then Parse Var Jc " JOB " "'" Account "'"
    Parse Var Account Account ','
    /* adjust this to see if user has a valid account code in the */
    /* job card. don't want to accept a default job card.         */
    If Length(Account) <> 7 Then Leave
  End
  Call AddCard Jc
End

/******************************************************************/
/* the user didn't have a valid job card, build the default.      */
/******************************************************************/
If Out.0 = 0 Then Do
  Name = Sysvar('SYSUID') || Action
  Call AddCard "//"Name "JOB (1005302),'DELETE/RENAME FILES',"
  Call AddCard "//         MSGCLASS=X,CLASS=A"
End

/******************************************************************/
/* build the idcams step.                                         */
/******************************************************************/
Call AddCard "//*"
Call AddCard "//*  RESTART PROCESSING FOR JOB:" JOBNAME "JOBID:" JOBID
Call AddCard "//*"
Call AddCard "//DELREN   EXEC PGM=IDCAMS"
Call AddCard "//SYSPRINT  DD  SYSOUT=*"
Call AddCard "//SYSIN     DD  *"

/******************************************************************/
/* add the datasets to delete.                                    */
/******************************************************************/
Llq = 'D'Left(DATE('O'),2) || ,
   Substr(Date('O'),4,2) || ,
   Right(Date('O'),2)

Do iStep = 1 To Step.0
  If Dsn.iStep.0 > 0 Then
    Call AddCard "  /* DSNS FROM" Step.iStep "*/"
  Do iDsn = 1 To Dsn.iStep.0
    DsnStatus = Strip(Left(Sysdsn("'"Dsn.iStep.iDsn"'"),20))
    If DsnStatus <> 'OK' & DsnStatus <> 'VOLUME NOT ON SYSTEM' Then ,
      Call AddCard "  /* !!!!! STATUS FOR THE FOLLOWING IS:" DsnStatus "*/"
    If Action = 'R' Then Do
      Call AddCard "  ALTER" Dsn.iStep.iDsn "-"
      NewDsn = Dsn.iStep.iDsn"."Llq
      If Length(NewDsn) > 44 Then
        Call AddCard "  /*     !!!!! THE FOLLOWING DSN IS TOO LONG */ -"
      Call AddCard "    NEWNAME("NewDsn")"
    End
    Else Call AddCard "  DELETE" Dsn.iStep.iDsn
  End
  If Dsn.iStep.0 > 0 Then Call AddCard " "
End

/******************************************************************/
/* finish job                                                     */
/******************************************************************/
Call AddCard "/*"
Call AddCard "//"

/******************************************************************/
/* build a temp file to write out the job.                        */
/******************************************************************/
OutFile = "'"Sysvar('SYSUID')".RESTART.T"Time(Seconds)"'"
x = Msg('OFF')
"DELETE" OutFile
x = Msg(x)

Address TSO
"ALLOC F(OUT) DA("OutFile") NEW CATALOG" ,
  "SPACE(3 2) TRACKS" ,
  "LRECL(80) DSORG(PS) RECFM(F B)"
"EXECIO * DISKW OUT (STEM Out. FINIS)"
"FREE F(OUT)"

/******************************************************************/
/* edit it and delete the temp file, user must submit it.         */
/******************************************************************/
Address IOF
"TSIEXEC SELECT CMD(%FDSN "OutFile" E) NEWAPPL(ISR)"

Address TSO
x = Msg('OFF')
"DELETE" OutFile
x = Msg(x)

Address IOF
"TSIMSG INFO 'Complete' 'The restart command is complete'"
RC = 0
Return 0
/******************************************************************/
/* add a card to the job cards deck.                              */
/******************************************************************/
AddCard:
Arg Card

iOut = Out.0 + 1
Out.0 = iOut
Out.iOut = Card

Return
