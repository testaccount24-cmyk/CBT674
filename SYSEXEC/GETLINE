/* rexx */
/******************************************************************/
/* this will return info on the line of the ispf screen that the  */
/* cursor is currently parked on. it is useful for passing parms  */
/* to an exec based on what the user is pointing to. this will    */
/* vput the fields that are in all caps to pass the data.         */
/******************************************************************/
Address ISPEXEC

Tcb    = Ptr(540)               /* TCB (EXEC command)    PSATOLD  */
Tcb    = Ptr(Tcb+132)           /* TCB (ISPTASK)         TCBOTC   */
Fsa    = Ptr(Tcb+112)           /* first save area       TCBFSA   */
R1     = Ptr(Fsa+24)            /* ISPTASK's R1                   */
Tld    = Ptr(R1)                /* TLD address                    */
Tls    = Ptr(Tld+096)           /* screen buffer         TLDTLSP  */
CSR    = Ptr(Tld+164)           /* relative cursor pos.  TLDCSR   */
SCRW   = Ptr(Tld+192)           /* screen width          TLDCLSWD */

Panelad= D2X(Tld+344)           /* address of the panelid         */
PANEL  = Storage(Panelad,8)     /* name of the panel              */

If SCRW>0 Then CSRP = CSR//SCRW+1 /* cursor position in LINE      */
Else CSRP = 1                   /* ISPSTRT                        */

Linead = D2X(Tls+CSR-CSRP+1)    /* address of current LINE        */
LINE   = Storage(Linead,SCRW)   /* text of current LINE           */
Valid  = Copies(' ',64)||Xrange('40'x,'FF'x)
LINE   = Translate(LINE,Valid)

"VPUT (CSR CSRP LINE SCRW PANEL) SHARED"

"VGET GETLINE SHARED"
If GETLINE = 'DEBUG' Then Do
  Say 'Tcb='D2X(Tcb) ,
      'Fsa='D2X(Fsa) ,
      'R1='D2X(R1),
      'Tld='D2X(Tld) Storage(D2X(Tld),4)
  Say 'Tls='D2X(Tls)
  Say 'CSR='CSR ,
      'CSRP='CSRP ,
      'SCRW='SCRW ,
      'Linead='Linead
  Say 'LINE='LINE
  Say 'Dumping Tld...'
  Call DumpStor D2X(Tld) 400
  Say 'Dumping screen...'
  Call DumpStor D2X(Tls) 200
End

Return
Ptr: Return C2D(Bitand(Storage(D2X(Arg(1)),4),'7FFFFFFF'x))
