/* rexx */
/*********************************************************************/
/* used with the 'startup' exec to start a bunch of ispf screens.    */
/* allocate a dataset called userid.SS.COMMANDS, and place in this   */
/* dataset one line for each startup command you want started.       */
/*********************************************************************/
Address TSO
StartDsn = "'"Sysvar('SYSUID')".SS.COMMANDS'"
If Sysdsn(StartDsn) <> 'OK' Then Do
  Say StartDsn 'not found.'
  Return
End

If Listdsi(StartDsn) > 4 Then Do
  Say StartDsn '- Undetermined error.'
  Return
End

If SYSDSORG <> 'PS' Then Do
  Say StartDsn '- Bad Dsorg ('SYSDSORG')'
  Return
End

"ALLOC F(IN) DA("Startdsn") SHR REUSE"
"EXECIO * DISKR IN (STEM SS. FINIS"
"FREE F(IN)"

x = Outtrap('Scn.')
Call Screens                    /* get list of active app names */
x = Outtrap('OFF')
Do iScn = 1 To Scn.0
  Parse Var Scn.iScn 'Screen name=' ScnName 49 .
  ScnName = Strip(ScnName)
  Scn.iScn = ScnName                    /* save only screen name     */
End

Address ISPEXEC
Do iSS = 1 To SS.0
  SS.iSS = Strip(SS.iSS)
  Do iScn = 1 To Scn.0
    If Scn.iScn = SS.iSS Then Do
      Say SS.iSS 'already started.'
      Iterate iSS
    End
  End
  Say 'Starting' SS.iSS
  "SELECT PGM(ISPSTRT) PARM(CMD(%STARTUP" SS.iSS"~SWAP~END) SUSPEND)"
End

Return
