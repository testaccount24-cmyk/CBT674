/* rexx */
Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN DumpTab,Source
Return
/*-------------------------------------------------------------------*/
/*- this will convert the dump log to an ispf table for manipulation-*/
/*-------------------------------------------------------------------*/
Main_Routine:

/* create a unique table name based on the session number to allow   */
/* table viewing in multiple split screens.                          */
Address ISPEXEC
"VGET ZSCREEN SHARED"          /* this will allow split screen */
DumpTab = 'SVCDUMP'ZSCREEN

/* if you have nested calls to the ispf interface, tell the user     */
/* all about it and get out. i guess i could allow the table to      */
/* display, but i'd have to access the original timeopened and make  */
/* sure i don't end the table on return etc. too much work.          */
"LIBDEF ISPTLIB LIBRARY ID(ISPPROF) STACK"
"TBSTATS" DumpTab "STATUS3(STATUS3)"
If Status3 = 2 Then Do                /* can't access table      */
  Say 'The SVC dump table is currently unavailable.'
  Say 'You may have already opened it in this ISPF split screen.'
  Return 4
End

TimeOpened = DUMPSTMP()
"TBCREATE" DumpTab "KEYS(DATE TIME SEQ)" ,
  "NAMES(JOB STATUS HOLD TITLE STAMP) NOWRITE"

/* read in the dump log so we can convert. */
Address TSO
If DUMPLOG("SHR") = 12 Then Signal Failure
"EXECIO * DISKR LOG (STEM Log. FINIS"
"FREE F(LOG)"

/* do the conversion. */
Address ISPEXEC
Do iLog = 3 To Log.0   /* skip headings */
  Parse Var Log.iLog Date Time Seq Job 29 30 Status 34 ,
    35 Hold 36 37 Title 137 Stamp .
  "TBADD" DumpTab
End

Return DumpTab TimeOpened
