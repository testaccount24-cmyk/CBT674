/* rexx */
Arg DumpTab TimeOpened .

Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN DumpTab,Source
Return
/*-------------------------------------------------------------------*/
/*- this will convert the ispf table back to the dump log.          -*/
/*-------------------------------------------------------------------*/
Main_Routine:

/* only process those entries that have been changed since opening.  */
Digits = Digits()
Numeric Digits Length(DUMPSTMP())
Address ISPEXEC
"TBTOP" DumpTab
Do Forever
  "TBSKIP" DumpTab "NUMBER(1)"
  If RC > 0 Then Leave
  If Stamp < TimeOpened Then "TBDELETE" DumpTab
End
Numeric Digits Digits

/* if there's nothing left to process, don't bother updating the log*/
"TBSTATS" DumpTab "ROWCURR(ROWS)"
If Rows > 0 Then Do
  If DUMPLOG("OLD") = 12 Then Signal Failure
  Call UpdateLog
  Address TSO
  "EXECIO * DISKW LOG (STEM Out. FINIS"
  "FREE F(LOG)"
End
Address ISPEXEC
"TBEND" DumpTab
"LIBDEF ISPTLIB"

Return 0
/*-------------------------------------------------------------------*/
/*- The main exec routine.                                          -*/
/*-------------------------------------------------------------------*/
UpdateLog:

/* copy the heading lines to the output stems */
Address TSO
"EXECIO 2 DISKR LOG"
iOut = 0
Do Queued()
  iOut = iOut + 1
  Pull Out.iOut
End
Out.0 = iOut

/* copy any updated log entries to the new log */
Address ISPEXEC
Do Forever
  Address TSO "EXECIO 1 DISKR LOG"
  If RC > 0 Then Leave
  Pull In
  Parse Var In Date Time Seq LJob 29 30 LStatus 34 ,
    35 LHold 36 37 LTitle 137 138 LStamp .

  "TBTOP" DumpTab
  "TBGET" DumpTab
  /* if not found, or if found but log time is greater (which can   */
  /* happen if someone else was processing the dump log while you   */
  /* were messing with it), simply copy the log record back to the  */
  /* log. only the record with the highest timestamp will be used.  */
  If RC > 0 | (RC = 0 & Stamp < LStamp) Then Do
    If RC = 0 Then "TBDELETE" DumpTab
    iOut = iOut + 1
    Out.iOut = In
    Iterate
  End

  /* if this entry was deleted, don't bother to copy it. make sure  */
  /* all the possible quals are deleted to keep in sync with catalog*/
  If HOLD = 'D' Then Do
    Call DeleteDump
    "TBDELETE" DumpTab
    Iterate
  End

  /* take the table entry values and override the log entry.        */
  iOut = iOut + 1
  Out.iOut = DUMPFREC(Date,Time,Seq,Job,Status,Hold,Title,Stamp)
  "TBDELETE" DumpTab
End
Out.0 = iOut
Address TSO "EXECIO 0 DISKR LOG (FINIS"

/* add in any table entries that have not been handled above. this  */
/* can happen during a sync when catalog entries are found for which*/
/* there are no log entries.                                        */
"TBTOP" DumpTab
iOut = Out.0
Do Forever
  "TBSKIP" DumpTab "NUMBER(1)"
  If RC > 0 Then Leave
  If HOLD = 'D' Then Do
    Call DeleteDump
    Iterate
  End
  iOut = iOut + 1
  Out.iOut = DUMPFREC(Date,Time,Seq,Job,Status,Hold,Title,Stamp)
End
Out.0 = iOut

Return
/*-------------------------------------------------------------------*/
/*- delete all the bits that have to do with the dump.              -*/
/*-------------------------------------------------------------------*/
DeleteDump:

Quals = GETDQUAL()
Prefix = GETDPFX(Date,Time,Seq)
x = Msg('OFF')
Do While Words(Quals) > 0
  Parse Var Quals Qual Quals
  Address TSO "DELETE '"Prefix'.'Qual"' PURGE"
End
x = Msg(x)

Return
/*-------------------------------------------------------------------*/
/*- Only for debugging purposes.                                    -*/
/*-------------------------------------------------------------------*/
SayTab:

Say 'Saying table...'
"TBTOP" DumpTab
Do Forever
  "TBSKIP" DumpTab "NUMBER(1)"
  If RC > 0 Then Leave
  Say Date Time Seq Job Title Stamp
End
Say 'Say table complete'

Return
