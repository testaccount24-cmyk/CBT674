/* rexx */
Arg Prefix .

Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN 'NOTAB',Source
Return
/*-------------------------------------------------------------------*/
/*- this will construct an archive job. used by dumparch and dumpispf*/
/*-------------------------------------------------------------------*/
Main_Routine:

/* get a list of datasets that will be archived to tape. */
iDsn = 0
Quals = GETDQUAL()
Excl = GETDEXCL() 'TAPE'
Do While Words(Quals) > 0
  Parse Var Quals Qual Quals
  If WordPos(Qual,Excl) > 0 Then Iterate
  Dsn = Prefix'.'Qual
  If Sysdsn("'"Dsn"'") = 'DATASET NOT FOUND' Then Iterate
  iDsn = iDsn + 1
  Dsn.iDsn = Dsn
End
Dsn.0 = iDsn

/* construct the job to archive the datasets. */
Address ISPEXEC
"FTOPEN TEMP";     If RC <> 0 Then Signal Failure
"FTINCL JOBCARD";  If RC <> 0 Then Signal Failure
"FTINCL ARCH1";    If RC <> 0 Then Signal Failure

Do iDsn = 1 To Dsn.0
  Dsn = Dsn.iDsn
  "FTINCL ARCH2";  If RC <> 0 Then Signal Failure
End
"FTINCL ARCH3";    If RC <> 0 Then Signal Failure
Do iDsn = 1 To Dsn.0
  Dsn = Dsn.iDsn
  "FTINCL ARCH4";  If RC <> 0 Then Signal Failure
End

"FTINCL ARCH5";    If RC <> 0 Then Signal Failure
"FTCLOSE";         If RC <> 0 Then Signal Failure

Return 0
