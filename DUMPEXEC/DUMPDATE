/* rexx */
Arg DumpIn .

Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN 'NOTAB',Source
Return
/*-------------------------------------------------------------------*/
/*- this exec will return the date and time captured for a dump     -*/
/*- dataset by displaying them via the 'd d,t' command. pass the    -*/
/*- dsn you are interested in like sys1.dump00 or just dump00.      -*/
/*-------------------------------------------------------------------*/
Main_Routine:
/*IEE853I 14.06.31 SYS1.DUMP TITLES 080
  SYS1.DUMP DATA SETS AVAILABLE=002 AND FULL=001
  CAPTURED DUMPS=0000, SPACE USED=00000000M, SPACE FREE=00000500M
       DUMP00 TITLE=ZEBB02A ESTAE dump
           DUMP TAKEN TIME=10.32.08 DATE=03/30/2004
  NO DUMP DATA AVAILABLE FOR THE FOLLOWING EMPTY SYS1.DUMP DATA SETS:
  01-02
*/

Say '>>>>>DDTDATE: Resolving date/time for' DumpIn

/* make sure we have a valid dsn */
DumpIn = Strip(DumpIn,"B","'")
If Left(DumpIn,5) = 'SYS1.' Then DumpIn = Substr(DumpIn,6)
Parse Var DumpIn 'DUMP' Dump .
If Datatype(Dump) <> 'NUM' | Length(Dump) <> 2 Then Signal Failure

/* issue the command via the console tso/e service or opsmvs command */
Msg.0 = 0
How = GETDOPER()
If How = 'CONSOLE' Then Call GetFromConsole
Else If How = 'OPSMVS' Then
  "OPSCMD COMMAND('D D,T') STOPEND(YES) CMDRESP(REXX) PREFIX(MSG)"

/* play with the results. look for the dsn we're interested in. */
If Msg.0 = 0 Then Return CurrentDateTime()
Found = 0
Parse Value '' With Title Date Time
Do iMsg = 1 To Msg.0
  If Found = 0 Then Do
    Parse Var Msg.iMsg 6 Dsn ' TITLE=' Title
    If Dsn = DumpIn Then Do
      Found = 1
      Iterate
    End
  End
  If Found Then Do
    Parse Var Msg.iMsg 'DUMP TAKEN TIME=' Time ' DATE=' Date .
    If Date = '' Then Iterate
    Parse Var Time HH '.' MM '.' SS
    Time = 'T' || HH || MM || SS
    Parse Var Date MO '/' DA '/' YR
    YR = Right(YR,2)
    Date = 'D' || YR || MO || DA
    Leave
  End
End
If Date = '' Then Return CurrentDateTime()

Say '>>>>>DDTDATE: Resolved to' Date Time 'for dump' Title

Return Date Time Title
/*-------------------------------------------------------------------*/
/*- the console command failed. assume the dump occured right now   -*/
/*- and return the current date and time.                           -*/
/*-------------------------------------------------------------------*/
CurrentDateTime:

Date = 'D'Right(Date('S'),6)
Parse Value Time('N') With HH ':' MM ':' SS
Time = 'T'HH || MM || SS

Say '>>>>>DDTDATE: Unable to determine dump date/time from display' ,
    'operator command (D D,T).'
Say '>>>>>DDTDATE: Defaulted to' Date Time '(current date/time)'
Say '>>>>>DDTDATE: Dump title is unknown'

Return Date Time 'UNKNOWN'
/*-------------------------------------------------------------------*/
/*- get the dump date/time from the tso console command.            -*/
/*-------------------------------------------------------------------*/
GetFromConsole:

SaveDisp = Sysvar('SOLDISP')
"CONSPROF SOLDISPLAY(NO)"
If RC > 0 Then Return CurrentDateTime()
"CONSOLE ACTIVATE"

GetMsg = 0
iMsg = 0
"CONSOLE SYSCMD(D D,T) CART(DDT)"
Do While GetMsg = 0
  GetMsg = GetMsg('Resp.','SOL','DDT',,10)
  If GetMsg = 0 Then Do iResp = 1 To Resp.0
    iMsg = iMsg + 1
    Msg.iMsg = Resp.iResp
  End
End
Msg.0 = iMsg

"CONSOLE DEACTIVATE"
"CONSPROF SOLDISPLAY("SaveDisp")"

Return
