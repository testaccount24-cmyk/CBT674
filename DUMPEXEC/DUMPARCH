/* rexx */
Arg ArchDays .

Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN DumpTab,Source
Return
/*-------------------------------------------------------------------*/
/*- this will run the archive for the dumps. pass the number of     -*/
/*- days to archive.                                                -*/
/*-------------------------------------------------------------------*/
Main_Routine:

If ArchDays = '' | Datatype(ArchDays) <> 'NUM' Then Signal Failure

/* convert the dump log to an ispf table.                            */
Parse Value LOG2TAB() With DumpTab TimeOpened .
If DumpTab = 4  Then Return 4
If DumpTab = 12 Then Signal Failure

Say '>>>>>DUMPARCH: Archiving dumps over' ArchDays 'days old'

Address ISPEXEC
"TBTOP" DumpTab
Do Forever
  "TBSKIP" DumpTab "NUMBER(1)"
  If RC > 0 Then Leave

  If Hold = 'H' Then Iterate

  Prefix = GETDPFX(Date,Time,Seq)
  Parse Value DUMPSTAT(Prefix) With Status1 Status2 .
  If Status1 = 'TAPE' Then Iterate

  /* calculate the number of days old this dump is.                  */
  Date2 = Left(Date('S'),2) || Date              /* make four digits */
  Days = Date('B') - Date('B',Date2,'S')
  If Days <= ArchDays Then Iterate

  /* see if this dump has already been archived once.                */
  If Status1 = 'DISK' & Status2 = 'TAPE' Then Call DeleteRecalled
  Else Call ArchiveJob

  /* update our table.                                               */
  Status = 'TAPE'
  Stamp = DUMPSTMP()
  "TBMOD" DumpTab
End

/* copy the table back to the dump log.                              */
Call TAB2LOG DumpTab TimeOpened

Say '>>>>>DUMPARCH: Completed archive cycle'

Return 0
/*-------------------------------------------------------------------*/
/*- remove recalled datasets for previously archived dumps.         -*/
/*-------------------------------------------------------------------*/
DeleteRecalled:

Say '>>>>>DUMPARCH:' Prefix'.DUMP has been previously archived' ,
  'and recalled. Deleting recalled datasets'

Quals = GETDQUAL()
Excl = GETDEXCL() 'TAPE'               /* just to make sure */
Do While Words(Quals) > 0
  Parse Var Quals Qual Quals
  If WordPos(Qual,Excl) > 0 Then Iterate
  Dsn = Prefix'.'Qual
  If Sysdsn("'"Dsn"'") = 'DATASET NOT FOUND' Then Iterate
  Address TSO "DELETE '"Dsn"'"
End

Return
/*-------------------------------------------------------------------*/
/*- submit a job to archive the dump to tape.                       -*/
/*-------------------------------------------------------------------*/
ArchiveJob:

Say '>>>>>DUMPARCH:' Prefix'.DUMP is' Days 'days old'

/* construct and submit the dump arch job. the dumparcj is used      */
/* by dumpispf as well, that's why it's an external routine.         */
If DUMPARCJ(Prefix) = 12 Then Signal Failure
"VGET ZTEMPN SHARED"; If RC <> 0 Then Signal Failure
Address TSO
"EXECIO * DISKR" ZTEMPN "(FINIS"
Queue ''
"SUBMIT *"
Address ISPEXEC

Return
