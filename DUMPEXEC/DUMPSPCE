/* rexx */
Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN 'NOTAB',Source
Return
/*-------------------------------------------------------------------*/
/*- this will return a volume with the most freespace for pooling.  -*/
/*-------------------------------------------------------------------*/
Main_Routine:

Env = Sysvar('SYSENV')
Vols = GETDVOL()
If Words(Vols) = 1 Then Do
  Call LogMsg 'Only one volume found in exec GETDVOL().' ,
    'Volume pooling will not be utilized.'
  Return Vols
End

/* using MXI, get the free space for each volume and save the one    */
/* with the most free space.                                         */
MaxVol = ''
MaxFree = 0
Do While Length(Vols) > 0
  Parse Var Vols Vol Vols
  Free = ''
  MxiRC = MxiRexx('Vtoc.','NOTITLES','DASD' Vol 'NOT(SMS)')
  If MxiRC = 0 Then Do
    If Vtoc.0 = 0 Then Do
      Call LogMsg 'Volume' Vol 'not found.'
      Iterate
    End

    If Vtoc.0 > 15 Then Parse Var Vtoc.15 'Free Tracks' Free .
    If Free = '' Then Do
      Call LogMsg 'Free tracks not found on line 15 of MXI',
        'DASD command. Update this exec to increase performance.'
      Do iVtoc = 1 To Vtoc.0
        Parse Var Vtoc.iVtoc 'Free Tracks' Free .
        If Free <> '' Then Leave
      End
    End

    If Free = '' Then Do
      Call LogMsg 'Unable to locate "Free Tracks" in MXI volume report.'
      Iterate
    End
    If Free > MaxFree Then Do
      MaxFree = Free
      MaxVol = Vol
    End
  End
  Else Call LogMsg 'MxiRexx RC=' MxiRC 'for volume' Vol
End

If MaxVol = '' Then Do
  Call LogMsg 'Unable to find the largest freespace volume.' ,
    'Defaulting to first volume in exec GETDVOL().'
  MaxVol = Word(GETDVOL(),1)
End

Return MaxVol
/*-------------------------------------------------------------------*/
/*- just log messages for batch processes.                          -*/
/*-------------------------------------------------------------------*/
LogMsg:
Arg Msg

If Env = 'FORE' Then Return
Say '>>>>>DUMPSPCE:' Msg

Return
