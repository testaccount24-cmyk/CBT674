/* rexx */
Arg PurgeDays .

Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN DumpTab,Source
Return
/*-------------------------------------------------------------------*/
/*- purge any dumps over a number of days old.                      -*/
/*-------------------------------------------------------------------*/
Main_Routine:

If PurgeDays = '' | Datatype(PurgeDays) <> 'NUM' Then Signal Failure

/* convert the log to a table. */
Parse Value LOG2TAB() With DumpTab TimeOpened .
If DumpTab = 4  Then Return 4
If DumpTab = 12 Then Signal Failure

Say '>>>>>DUMPPURG: Purging dumps over' PurgeDays 'days old'

Address ISPEXEC
"TBTOP" DumpTab
Do Forever
  "TBSKIP" DumpTab "NUMBER(1)"
  If RC > 0 Then Leave

  If Hold = 'H' Then Iterate

  Date2 = Left(Date('S'),2) || Date    /* make four digits */
  Days = Date('B') - Date('B',Date2,'S')
  If Days <= PurgeDays Then Iterate

  Prefix = GETDPFX(Date,Time,Seq)
  Say '>>>>>DUMPPURG: Deleting' Prefix'.*.' Days 'days old'
  Hold = 'D'
  Stamp = DUMPSTMP()
  "TBMOD" DumpTab
End

Call TAB2LOG DumpTab TimeOpened

Say '>>>>>DUMPPURG: Completed purge cycle'

Return 0
