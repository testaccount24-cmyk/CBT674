/* rexx */
/* a little rexx to notify users that a dump occured. you can update */
/* this to whatever process you want, such as email or ftping a log  */
/* or sending an snmp trap (if you have the underlying software      */
/* properly configured).                                             */
Arg Date,Time,Job,Title

/* get a list of userids to notify. if you change this program to    */
/* an email, you can change getdntfy to return to: and cc: stuff.    */
/* getdntfy should return appropriate routing information on the     */
/* external data queue that this rexx will be able to interpret.     */
/* alternatively you can do everything here and dispense with        */
/* getdntfy. i just put it there to keep customization consistent.   */
Call GETDNTFY
If Queued() <> 1 Then Do
  /* only want one record for tso notify */
  Say '>>>>>DUMPNTFY: Expected 1 record on data stack, got' ,
    Queued()'. Notify failed'
  Do Queued(); Pull; End;
  Return
End
Pull Userids

/* convert the date and time to something more palatteable. this will*/
/* fail during rollover in the year 2100. send me an inter-galactic  */
/* teleportaion contract for $1,000,000 credits/day + expences at    */
/* that time, and i'll fix it. it may take several weeks of work tho.*/
Date = Left(Date('S'),2) || Date
Date = Date('N',Date,'S')
Parse Var Time HH 3 MM 5 SS
Time = HH':'MM':'SS

/* send the users a message.                                         */
Address TSO
"SEND 'An SVC Dump was taken on" Date Time "for job" Job"'" ,
  "USER("Userids") LOGON"
"SEND '  Title:" Strip(Left(Title,55))"'" ,
  "USER("Userids") LOGON"

Return
