/* rexx */
Arg Disp .

Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN 'NOTAB',Source
Return
/*-------------------------------------------------------------------*/
/*- This will allocate the dump log with the disp passed. if alloc  -*/
/*- fails, it is retried until it gains access to it.               -*/
/*-------------------------------------------------------------------*/
Main_Routine:

/* get the name of the log  and make sure it is allocated */
Log = GETDLOG()
Call AllocLog

Address TSO
Do Retries = 1 To 20     /* 20 retries */
  "ALLOC F(LOG) DA('"Log"')" Disp
  If RC = 0 Then Leave

  /* if foreground prompt the user */
  If Sysvar('SYSENV') = 'FORE' Then Do
    Address ISPEXEC
    "ADDPOP"
    "DISPLAY PANEL(SDWAIT)"
    SaveRC = RC
    "REMPOP"
    Address TSO
    If SaveRC > 0 Then Signal Failure
    Retries = 1       /* keep prompting until user gives up */
  End
  /* if background wait and try again */
  Else Do
    Say '>>>>>DUMPLOG: Waiting for access to' Log
    If GETDWAIT() > 0 Then Signal Failure
  End
End
If Retries > 20 Then Signal Failure
If Retries > 1 & Sysvar('SYSENV') = 'BACK' Then
  Say '>>>>>DUMPLOG: Gained access to log after' Retries 'retries'

Return 0
/*-------------------------------------------------------------------*/
/*- allocate a new log.                                             -*/
/*-------------------------------------------------------------------*/
AllocLog:

Log = Strip(Log,"B","'")
Sysdsn = Sysdsn("'"Log"'")
If Sysdsn = "DATASET NOT FOUND" Then Do
  Say '>>>>>DUMPLOG: Allocating log file' Log

  "NEWSTACK"              /* in case there's anything queued up */
  Queue DUMPFREC("YYMMDD","HHMMSS","Seq","Job","Stat","H", ,
    "Title","Time Stamp")
  Queue DUMPFREC("------","------","-----","--------","----","-", ,
    Copies('-',100),"------------------")

  "ALLOC F(DUMPLOG) DA('"Log"') NEW CATALOG" ,
    "SPACE(10 10) TRACKS LRECL(155) RECFM(F B) DSORG(PS)" ,
    GETDALOC("TSO")
  "EXECIO" Queued() "DISKW DUMPLOG (FINIS"
  "FREE F(DUMPLOG)"
  "DELSTACK"
End
Else If Sysdsn = "UNAVAILABLE DATASET" Then Return
Else If Sysdsn <> "OK" Then Signal Failure

Return
