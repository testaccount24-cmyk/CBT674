/* rexx */
Arg DumpIn .

Signal On Failure
Signal On Syntax
Signal On Novalue

Return Main_Routine()
/*-------------------------------------------------------------------*/
/*- handle any problems encountered                                 -*/
/*-------------------------------------------------------------------*/
Failure: Say "Failure occured on line" Sigl; Call Cleanup; Exit(12);
Syntax:  Say "Syntax error on line"    Sigl; Call Cleanup; Exit(12);
Novalue: Say "Novalue used on line"    Sigl; Call Cleanup; Exit(12);
Cleanup:
Parse Source Source
Call DUMPCLN 'NOTAB',Source
Return
/*-------------------------------------------------------------------*/
/*- this will perform an analysis of the dump passed.               -*/
/*-------------------------------------------------------------------*/
Main_Routine:

DumpIn = Strip(DumpIn,"B","'")
If DumpIn = '' Then Signal Failure
/* if it's a sys1.dump dataset, then copy it to our naming standard  */
/* and clear the dump dataset for the next guy to use.               */
If Left(DumpIn,9) = 'SYS1.DUMP' & Length(DumpIn) = 11 Then Do
  DumpIn = DumpCopy(DumpIn)
  DumpIn = Strip(DumpIn,"B","'")
End
If DumpIn = 12 Then Signal Failure

Say '>>>>>DUMPANAL: Performing analysis on' DumpIn

/* clean up work datasets just in case they already exist.           */
Prefix = Substr(DumpIn,1,LastPos('.',DumpIn))
DDir = Prefix'DDIR'
Toc  = Prefix'ITOC'
Prt  = Prefix'IPRT'

x = Msg('OFF')
"DELETE '"Toc"'"
"DELETE '"Prt"'"
x = Msg(x)

/* analyze the dump using ipcs.                                      */
"%BLSCDDIR DSNAME("DDir")" GETDALOC("IPCS")
"ALLOC F(IPCSDUMP) DA('"DumpIn"') SHR"
"ALLOC F(IPCSTOC) DA('"Toc"') NEW CATALOG RELEASE" ,
  "SPACE(1 1) TRACKS" GETDALOC("TSO")
"ALLOC F(IPCSPRNT) DA('"Prt"') NEW CATALOG RELEASE" ,
  "SPACE(5 5) TRACKS" GETDALOC("TSO")

"%BLSCBSVA" DumpIn

"FREE F(IPCSDUMP,IPCSTOC,IPCSPRNT,IPCSDDIR)"

Say '>>>>>DUMPANAL: Analysis complete'

Return DumpIn
