/* rexx */
/* this will take a file name such as LO12345.txt and parse it to */
/* a valid pds member name LO12345 and set the xfer mode to text  */
/* or binary (for .BIN files). used with exec ftppds.             */
Parse Arg FileName .
If FileName = '.' | FileName = '..' Then Return
If FileName = 'ZPDSLIST.txt' Then Return

/* save file name to upper case var */
UpperFileName = FileName
Upper UpperFileName

/* make sure we can get it down to 8 chars or less */
Parse Var UpperFileName Member '.'
If Length(Member) > 8 Then Do
  Say 'Unable to parse' FileName 'down to 8 chars. Skipping.'
  Return
End

/* decide what type of xfer to make it. you can code whatever filters*/
/* you like here to process out particular files you don't want      */
/* to transfer or set modes based on different file extentions.      */
Select
  When Pos('.TXT',UpperFileName) > 0 Then Type = 'A'
  When Pos('.BIN',UpperFileName) > 0 Then Type = 'I'
  Otherwise Do
    Say 'Defaulting' FileName 'to binary type transfer'.
    Type = 'I'
  End
End

Queue 'TYPE' Type
Queue 'GET' FileName Member '(REPLACE'

Return
